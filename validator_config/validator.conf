# gp connect get care record validations ruleset
# NHS Interoperability Toolkit
#
#
#
# *****************************************************************************************

VALIDATION-RULESET-NAME FHIR REST GP Connect validations
VALIDATION-RULESET-VERSION 0.1
VALIDATION-RULESET-TIMESTAMP 20160713
VALIDATION-RULESET-AUTHOR Simon Farrow

VALIDATE urn:nhs:names:services:gpconnect:fhir:operation:gpc.getcarerecord
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:gpconnect:fhir:operation:gpc.getcarerecord' with the messages)
# wont work properly since this check is intended for DSTU3
#CHECK hapifhirvalidator

#Schema Check on whole message
#CHECK schema TKW_ROOT/contrib/DMS_Schema/FGM/Schemas/bundle.xsd

#Transform Checks
CHECK xslt TKW_ROOT/contrib/Common/xslt/blank_attribute_checker.xslt ERROR
ANNOTATION "WARNING ONLY - test shows as fail if XML contains blank attributes"

CHECK xslt TKW_ROOT/contrib/Common/xslt/IllegalCharacters.xslt ERROR
ANNOTATION "WS-STD-01: Toolkit Messages MUST use UTF-8 encoding. Illegal character check may flag up some valid unicode characters, as it is not an exhaustive check. It will locate the use of non-rendering unicode control characters within the payload, sometimes erroneously introduced."

IF xpathexists /fhir:Parameters
THEN
	CHECK sub validate_Request_Header

	CHECK http_header content-type contains application 
	ANNOTATION http-header content-type check

	CHECK hapifhirvalidator

	# request
	CHECK context_path matches ^.*/fhir/Patient/\$gpc\.getcarerecord$
	ANNOTATION Context path is conformant

	CHECK content xpathexists /fhir:Parameters
	ANNOTATION xpath content check

ELSE
	CHECK hapifhirvalidator
	#
	# response
	IF xpathexists /fhir:Bundle
	THEN
		CHECK content xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Composition
		ANNOTATION Success xpath content check
	ELSE
		CHECK content xpathexists /fhir:OperationOutcome
		ANNOTATION Check for OperationOutcome
	END IF
END IF

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:read:metadata
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:gpconnect:fhir:rest:read:metadata-1 with the messages)
# DSTU2
#CHECK hapifhirvalidator

IF xpathequals boolean(/*[contains(local-name(),'Conformance') or contains(local-name(),'OperationOutcome')]) true
THEN
	## response success 
	#Schema Check on whole message
	#CHECK schema TKW_ROOT/contrib/DMS_Schema/FGM/Schemas/bundle.xsd
	#ANNOTATION Schema check (incorrect schema!)

	#Transform Checks
	#CHECK xslt TKW_ROOT/contrib/Common/xslt/blank_attribute_checker.xslt ERROR
	#ANNOTATION "WARNING ONLY - test shows as fail if XML contains blank attributes"

	#CHECK xslt TKW_ROOT/contrib/Common/xslt/IllegalCharacters.xslt ERROR
	#ANNOTATION "WS-STD-01: Toolkit Messages MUST use UTF-8 encoding. Illegal character check may flag up some valid unicode characters, as it is not an exhaustive check. It will locate the use of non-rendering unicode control characters within the payload, sometimes erroneously introduced."

	CHECK hapifhirvalidator

	IF xpathexists /fhir:Conformance
	THEN
		CHECK xpathexists /fhir:Conformance/fhir:publisher
		ANNOTATION Get metadata success check
	ELSE
		## response fail
		CHECK http_header content-type contains application 
		ANNOTATION Get metadata response fail content-type is correct
	END IF
	#
ELSE
	CHECK sub validate_Request_Header

	# assume its a request if here
	CHECK context_path matches ^.*/fhir/metadata$
	ANNOTATION Request context path
END IF


VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:read:metadata-1
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:gpconnect:fhir:rest:read:metadata-1 with the messages)
# Now for DSTU3

IF xpathequals boolean(/*[contains(local-name(),'CapabilityStatement') or contains(local-name(),'OperationOutcome')]) true
THEN

	CHECK hapifhirvalidator
	## response success 
	#Schema Check on whole message
	#CHECK schema TKW_ROOT/contrib/DMS_Schema/FGM/Schemas/bundle.xsd
	#ANNOTATION Schema check (incorrect schema!)

	#Transform Checks
	#CHECK xslt TKW_ROOT/contrib/Common/xslt/blank_attribute_checker.xslt ERROR
	#ANNOTATION "WARNING ONLY - test shows as fail if XML contains blank attributes"

	#CHECK xslt TKW_ROOT/contrib/Common/xslt/IllegalCharacters.xslt ERROR
	#ANNOTATION "WS-STD-01: Toolkit Messages MUST use UTF-8 encoding. Illegal character check may flag up some valid unicode characters, as it is not an exhaustive check. It will locate the use of non-rendering unicode control characters within the payload, sometimes erroneously introduced."
	IF xpathexists /fhir:CapabilityStatement
	THEN
		CHECK xpathexists /fhir:CapabilityStatement/fhir:publisher
		ANNOTATION Get metadata success check
	ELSE
		## response fail
		CHECK http_header content-type contains application 
		ANNOTATION Get metadata response fail content-type is correct
	END IF
	#
ELSE
	CHECK sub validate_Request_Header

	# assume its a request if here
	CHECK context_path matches ^.*/fhir/metadata$
	ANNOTATION Request context path
END IF

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:search:slot
#(remember to use 'VALIDATE-AS: urn:nhs:names:services:gpconnect:fhir:rest:search:slot with the messages)
# wont work proerly since this check is intended for DSTU3

IF xpathequals boolean(/*[contains(local-name(),'Bundle') or contains(local-name(),'OperationOutcome')]) true
THEN
	CHECK hapifhirvalidator

	IF xpathexists /fhir:Bundle
	THEN
		CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot
		ANNOTATION Get slots success check
	ELSE
		CHECK content xpathexists /fhir:OperationOutcome
		ANNOTATION Get slots fail check
	END IF
ELSE

	# assume its a request if here
	CHECK context_path matches ^.*/fhir/Slot.*$
	ANNOTATION Request context path
END IF

# Foundation 

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:read:patient-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Patient/[A-Za-z0-9\-\.]{1,64}$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:search:patient-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Patient\?.*$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:read:practitioner-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Practitioner/[A-Za-z0-9\-\.]{1,64}$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:search:practitioner-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Practitioner\?.*$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:read:organization-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Organization/[A-Za-z0-9\-\.]{1,64}$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:search:organization-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Organization\?.*$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:read:location-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Location/[A-Za-z0-9\-\.]{1,64}$

VALIDATE urn:nhs:names:services:gpconnect:fhir:operation:gpc.registerpatient-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Patient/\$gpc.registerpatient
CHECK hapifhirvalidator

# Appointment
VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:search:slot-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Slot.*$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:read:appointment-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Appointment/[A-Za-z0-9\-\.]{1,64}$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:create:appointment-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Appointment$
CHECK hapifhirvalidator

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:update:appointment-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Appointment/[A-Za-z0-9\-\.]{1,64}$
CHECK hapifhirvalidator

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:cancel:appointment-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Appointment/[A-Za-z0-9\-\.]{1,64}$

VALIDATE urn:nhs:names:services:gpconnect:fhir:rest:search:patient_appointments-1
CHECK sub validate_Request_Header
CHECK context_path matches ^.*/fhir/Patient/[0-9]+/Appointment\?.*$

# structured
VALIDATE urn:nhs:names:services:gpconnect:fhir:operation:gpc.getstructuredrecord-1
CHECK sub validate_Request_Header
CHECK http_header content-type contains application 
ANNOTATION http-header content-type check

CHECK context_path matches ^.*/fhir/Patient/\$gpc.getstructuredrecord

CHECK hapifhirvalidator
#
#==============================================================================
#
SUBSET validate_Request_Header1

	#CHECK jwt_payload xpathexists /json:json/json:requested_record/json:identifiers/json:identifier
	#ANNOTATION JWT xpath header check
	#
	# NB spec at https://tools.ietf.org/html/rfc6750 says that there must be only one space after Bearer
	# accepts non url chars for padding check 3 patterns check here one or more = in the first block, second block or optional third sig block
	CHECK http_header Authorization notmatches ^Bearer\s([A-Za-z0-9\\+_-]+=+\.[A-Za-z0-9\\+_-]+=*\.[A-Za-z0-9\\+_-]*=*|[A-Za-z0-9\\+_-]+=*\.[A-Za-z0-9\\+_-]+=+\.[A-Za-z0-9\\+_-]*=*|([A-Za-z0-9\\+_-]+=*\.){2}[A-Za-z0-9\\+_-]+=+)$
	ANNOTATION JWT Base 64 blocks do not have padding

	# accepts padding chars for url encoding check since generally base 64 url can have padding but is not allowed in a JWT
	CHECK http_header Authorization matches ^Bearer\s([A-Za-z0-9=_-]+\.){2}[A-Za-z0-9=_-]*$
	ANNOTATION JWT Base 64 blocks are URL Encoded

	# accepts padding and non url chars for block count
	CHECK http_header Authorization matches ^Bearer\s([A-Za-z0-9\\+=_-]+\.){2}$
	ANNOTATION JWT does not contain a signature block

#
#==============================================================================
#
SUBSET validate_Request_Header

#################################################################
#Check contents of HTTP Request Header in this sub-routine
#################################################################

SET literal $Location VALIDATION_HTTP_OF_HEADERS


IF http_header Accept matches ^application/fhir\+xml$|^application/fhir\+json$|^application/fhir\+xml;charset=utf-8$|^application/fhir\+json;charset=utf-8$
THEN
	CHECK http_header Accept matches ^application/fhir\+xml$|^application/fhir\+json$|^application/fhir\+xml;charset=utf-8$|^application/fhir\+json;charset=utf-8$
	ANNOTATION "Accept http header valid value valid MIME-type"
ELSE
	IF http_header Accept matches ^application/xml\+fhir$|^application/json\+fhir$|^application/xml\+fhir;charset=utf-8$|^application/json\+fhir;charset=utf-8$
	THEN
		CHECK http_header Accept matches ^application/xml\+fhir$|^application/json\+fhir$|^application/xml\+fhir;charset=utf-8$|^application/json\+fhir;charset=utf-8$
		ANNOTATION "WARNING Accept http header valid  MIME-type is currently accepted (for backward compatibility DSTU2)"
	ELSE
		CHECK http_header Accept matches ^application/xml\+fhir$|^application/json\+fhir$|^application/fhir\+xml$|^application/fhir\+json$|^application/xml$|^application/json$|^text/json$
		ANNOTATION "Accept http header invalid value. Not one of application/fhir+xml, application/fhir+json, application/xml+fhir, application/json+fhir, application/fhir+xml;charset=utf-8, application/fhir+json;charset=utf-8, application/xml+fhir;charset=utf-8, application/json+fhir;charset=utf-8"
	ENDIF
ENDIF

#Authorizatiion
CHECK http_header Authorization matches ^.+$
ANNOTATION "Authorization is mandatory and holds the base64url encoded JSON web token required for audit on the spine"

#JWT Validation - MUST include Bearer in the header added 18 Oct 2018 - Chris Brown. 
IF http_header Authorization matches ^.+$
THEN
	CHECK http_header Authorization matches ^\s?Bearer\s+([a-zA-Z0-9\-_]+)\.([a-zA-Z0-9\-_]+)\.$
	ANNOTATION "Authorization - the JWT must be three base64url encoded strings separated by dots. The final section (signature) must be empty."
ENDIF


#fromASID
CHECK http_header SSP-From matches ^[0-9]{12}$
ANNOTATION "SSP-From ASID httpheader well formed"

#toASID
CHECK http_header SSP-To matches ^[0-9]{12}$
ANNOTATION "SSP-To ASID httpheader well formed"

#
# Used for JUnit Tests
#
SUBSET check_bundle

IF xpathexists /fhir:Bundle
THEN
	CHECK http_header content-type contains application 
	ANNOTATION Get slots success check

	CHECK xpathexists /fhir:Bundle/fhir:entry/fhir:resource/fhir:Slot
	ANNOTATION Get slots success check
ELSE
	CHECK content xpathexists /fhir:OperationOutcome
	ANNOTATION Get slots fail check
END IF

VALIDATE urn:nhs:names:services:gpconnect:fhir:operation:gpc.migratestructuredrecord-1
	CHECK hapifhirvalidator
